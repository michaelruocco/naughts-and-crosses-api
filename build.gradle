plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
    id "com.diffplug.spotless" version "6.22.0"
    id "com.github.ben-manes.versions" version "0.49.0"
    id "pl.allegro.tech.build.axion-release" version "1.15.5"
    id "org.sonarqube" version "4.4.1.3373"
    id "nebula.lint" version "18.1.0"
}

scmVersion {
    versionCreator "versionWithBranch"
    tag {
        prefix = "${name}-"
    }
}
project.version = scmVersion.version

subprojects {
    group "com.github.michaelruocco"

    ext {
        slf4jVersion = "2.0.9"
        lombokVersion = "1.18.30"

        junitVersion = "5.10.0"

        gitUrl = "https://github.com/michaelruocco/naughts-and-crosses-api"
        pomConfig = {
            licenses {
                license {
                    name "MIT License"
                    url "https://opensource.org/licenses/MIT"
                    distribution "repo"
                }
            }

            developers {
                developer {
                    id "mruoc"
                    name "Michael Ruocco"
                    email "michael.ruocco@hotmail.com"
                }
            }

            scm {
                url project.ext.gitUrl
            }
        }
    }
}

gradleLint {
    rules = ['all-dependency']
    alwaysRun = false
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "michaelruocco"
        property "sonar.projectKey", "michaelruocco_${name}"
        property "sonar.coverage.jacoco.xmlReportPaths", "app/spring/build/reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml"
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview"].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release candidate")
            }
        }
    }
}

nexusPublishing {
    packageGroup = "com.github.michaelruocco"
    repositories {
        sonatype {
            username = System.getenv("OSSRH_USERNAME")
            password = System.getenv("OSSRH_PASSWORD")
        }
    }
}
